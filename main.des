INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI

    root = Tk()
    root.title("A Watch")
    g = DWatchGUI(root, eventhandler)

    eventhandler.start()
    root.mainloop()
    exit()

STATECHART:
    DISPLAY[DS][CS]
        NORMAL_STATE [DS]
            RUNNING_TIME [DS]
        BOTTOM_RIGHT_BUTTON_HOLDING [CS]
        EDIT_TIME
            EDIT_MODE [DS]
            INCREASING_TIME
        CHRONO [HS]
            STOP [DS]
            RUNNING
    LIGHT[CS][DS]
        OFF[CS][DS]
        ON[CS]

# LIGHT
TRANSITION:
    S: LIGHT.OFF
    N: LIGHT.ON
    E: lightOn
    O: g.setIndiglo()

TRANSITION:
    S: LIGHT.ON
    N: LIGHT.OFF
    E: lightOff
    O: g.unsetIndiglo()

TRANSITION:
    S: DISPLAY.NORMAL_STATE.RUNNING_TIME
    N: DISPLAY.NORMAL_STATE.RUNNING_TIME
    T: 1
    O: g.updateRunningTime()

TRANSITION: [-1]
    S: DISPLAY.NORMAL_STATE
    N: DISPLAY.BOTTOM_RIGHT_BUTTON_HOLDING
    E: bottomRightPressed

TRANSITION: [0]
    S: DISPLAY.EDIT_TIME.EDIT_MODE
    N: DISPLAY.BOTTOM_RIGHT_BUTTON_HOLDING
    E: bottomRightPressed
    O: [DUMP("Press to stop editing")]

TRANSITION: [0]
    S: DISPLAY.BOTTOM_RIGHT_BUTTON_HOLDING
    N: DISPLAY.EDIT_TIME
    E: editTime
    O: [DUMP("Enter edit time mode")]

TRANSITION: [-1]
    S: DISPLAY.NORMAL_STATE.RUNNING_TIME
    N: DISPLAY.EDIT_TIME
    E: editTime
    O: [DUMP("Stop running time to edit")]

TRANSITION: [0]
    S: DISPLAY.BOTTOM_RIGHT_BUTTON_HOLDING
    N: DISPLAY.NORMAL_STATE
    E: finishEdit
    O: [DUMP("Stop editing time")]

TRANSITION: [-1]
    S: DISPLAY.EDIT_TIME.EDIT_MODE
    N: DISPLAY.NORMAL_STATE
    E: finishEdit
    O: [DUMP("Stop editing time")]

TRANSITION:
    S: DISPLAY.EDIT_TIME.EDIT_MODE
    N: DISPLAY.EDIT_TIME.INCREASING_TIME
    E: increase
    O: [DUMP("Start adjusting Time")]
       g.updateRunningTime()

TRANSITION:
    S: DISPLAY.EDIT_TIME.INCREASING_TIME
    N: DISPLAY.EDIT_TIME.INCREASING_TIME
    T: 0.3
    O: [DUMP("Adjusting Time")]
       g.updateRunningTime()

TRANSITION:
    S: DISPLAY.EDIT_TIME.INCREASING_TIME
    N: DISPLAY.EDIT_TIME.EDIT_MODE
    E: stopInc
    O: [DUMP("Stop adjusting Time")]

TRANSITION:
    S: DISPLAY.EDIT_TIME.EDIT_MODE
    N: DISPLAY.EDIT_TIME.EDIT_MODE
    T: 5
    O: [DUMP("Stop waiting for editing action")]
       g.waitingEditExpired()

TRANSITION: [HS]
    S: DISPLAY.NORMAL_STATE
    N: DISPLAY.CHRONO
    E: changeMode
    O: print "DISPLAY -> CHRONO"
       g.turnOffTimeDisplayMode()
       g.refreshChronoDisplay()
       g.backgroundChronoModeOff()

TRANSITION:
    S: DISPLAY.CHRONO.STOP
    N: DISPLAY.NORMAL_STATE
    E: changeMode
    O: print "CHRONO -> DISPLAY"
       g.turnOnTimeDisplayMode()
       g.refreshTimeDisplay()

TRANSITION:
    S: DISPLAY.CHRONO.RUNNING
    N: DISPLAY.NORMAL_STATE
    E: changeMode
    O: print "CHRONO -> DISPLAY, CHRONO BACKGROUND"
       g.turnOnTimeDisplayMode()
       g.refreshTimeDisplay()
       g.backgroundChronoModeOn()
       g.startThreadBackgroundChrono()

TRANSITION:
    S: DISPLAY.CHRONO.STOP
    N: DISPLAY.CHRONO.RUNNING
    E: initChrono
    O: print "CHRONO STARTED"

TRANSITION:
    S: DISPLAY.CHRONO.RUNNING
    N: DISPLAY.CHRONO.RUNNING
    T: 0.25
    O: g.chronoRunning()
       g.refreshChronoDisplay()

TRANSITION:
    S: DISPLAY.CHRONO.RUNNING
    N: DISPLAY.CHRONO.STOP
    E: initChrono
    O: print "CHRONO STOPPED"

TRANSITION:
    S: DISPLAY.CHRONO.STOP
    N: DISPLAY.CHRONO.STOP
    E: resetChrono
    O: g.resetChrono()
       print "CHRONO RESET"
       g.refreshChronoDisplay()




